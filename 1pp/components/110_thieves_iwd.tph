/* ================================================================================================================================================================================== *
 *  v 4.2.0                                                                                                                                                                           *
 *                                                  C O M P O N E N T   :   U n i q u e   T h i e f   A v a t a r s   f o r   I W D                                                   *
 *                                                                                                                                                                                    *
 * ================================================================================================================================================================================== *
 *  This component will patch your executable to support full thief animation sequences and install content for them (Unique animations for different thief armour levels).           *
 *  Note: IWD HoW .exe patching is only required if you'd like to have armour and chain mail on thieves.                                                                              *
 *  (leather and unarmoured work fine) | IWD II should not need it at all.                                                                                                            *
 * ================================================================================================================================================================================== *
 *  change-log:                                                                                                                                                                       *
 *  -----------                                                                                                                                                                       *
 *      - Coding simplification:                                                                                                                                                      *
 *          Copied entire folders instead of infinite lines (COPY ~1pp/folder/file.ext~ ~override~ or ACTION_FOR_EACH file IN list BEGIN COPY ~1pp/folder/file.ext~ ~override~ END).  *
 * ================================================================================================================================================================================== */

/* =============== *
 *  EXE PATCH IWD  *
 * =============== */

SILENT
OUTER_SET fail = 0
OUTER_SET found = 0

ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ || ~%WEIDU_OS%~ STRING_EQUAL_CASE ~unix~) BEGIN

	COPY ~idmain.exe~ ~idmain.exe~
		PATCH_IF (SOURCE_SIZE == 6873088) BEGIN	// is IDMain 142
			PATCH_PRINT @109000 // ~Icewind Dale v1.4.2 found (TotL).~
			PATCH_SILENT
			SET found = 1
			READ_BYTE "0x3D65DE" "check_r"
			PATCH_IF ("%check_r%" = 0x32) BEGIN
				WRITE_BYTE 0x3D65DE 0x34
			END ELSE BEGIN
				PATCH_PRINT ~Error: the value we are trying to replace at offset 0x3D65DE (%check_r%) does not match what was expected (32). Stopping installation.~
				SET fail = 1
			END
		END ELSE BEGIN
			PATCH_PRINT @100901 // ~Warning: Icewind Dale is not patched to 1.4.2. Trying 1.4.1...~
		END

	SILENT
	COPY ~idmain.exe~ ~idmain.exe~
		PATCH_IF (SOURCE_SIZE == 6840320) BEGIN	// is IDMain 141
			PATCH_PRINT @109002 // ~Icewind Dale v1.4.1 found (HoW only).~
			PATCH_SILENT
			SET found = 1
			READ_BYTE "0x3CF7D0" "check_r"
			PATCH_IF ("%check_r%" = 0x32) BEGIN
				WRITE_BYTE 0x3CF7D0 0x34
			END ELSE BEGIN
				PATCH_PRINT ~Error: the value we are trying to replace at offset 0x3CF7D0 (%check_r%) does not match what was expected (32). Stopping installation.~
				SET fail = 1
			END
		END ELSE BEGIN
			PATCH_PRINT @109003 // ~Game is not v1.4.1.~
		END

	ACTION_IF (fail = 1) BEGIN
		FAIL @103015 // ~Could not patch successfully.~
	END

	ACTION_IF (found != 1) BEGIN
		FAIL @109004 // ~Type of executable is unknown. Make sure your game is patched to 1.4.1 (HoW) or 1.4.2 (TotL).~
	END

END // of ACTION_IF


/* ================== *
 *  COPY NEW CONTENT  *
 * ================== */

PRINT @106002 // ~Copying new files.~
SILENT

/* --------------------------------------------------- *
 *  CDFT1 - Female unarmoured dwarven thief            *
 *  CDFT3 - Female dwarven thief with chain mail       *
 *  CDMT1 - Male unarmoured dwarven thief              *
 *  CDMT3 - Male dwarven thief with chain mail         *
 *  CEFT1 - Female unarmoured elven thief              *
 *  CEFT2 - Female elven thief with leather armour     *
 *  CEFT3 - Female elven thief with chain mail         *
 *  CEMT1 - Male unarmoured elven thief                *
 *  CEMT3 - Male elven thief with chain mail           *
 *  CHFT1 - Female unarmoured human thief              *
 *  CHFT2 - Female human thief with leather armour     *
 *  CHFT3 - Female human thief with chain mail         *
 *  CHMT1 - Male unarmoured human thief                *
 *  CHMT3 - Male human thief with chain mail           *
 *  CIFT1 - Female unarmoured halfling thief           *
 *  CIFT3 - Female halfling thief with chain mail      *
 *  CIMT1 - Male unarmoured halfling thief             *
 *  CIMT2 - Female halfling thief with leather armour  *
 *  CIMT3 - Male halfling thief with chain mail        *
 * --------------------------------------------------- */
COPY ~1pp/core/1pp_thieves_galore/files/cdft1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cdft3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cdmt1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cdmt3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/ceft1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/ceft2~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/ceft3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cemt1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cemt3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/chft1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/chft2~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/chft3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/chmt1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/chmt3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cift1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cift3~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cimt1~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cimt2~ ~override~
COPY ~1pp/core/1pp_thieves_galore/files/cimt3~ ~override~


/* ============ *
 *  PAPERDOLLS  *
 * ============ */

PRINT @110000 // ~Installing paperdolls (IWD)...~
SILENT

ACTION_FOR_EACH paperdoll IN ~cdft1inv~ ~cdft2inv~ ~cdft3inv~ ~cdmt1inv~ ~cdmt2inv~ ~cdmt3inv~ ~ceft1inv~ ~ceft2inv~ ~ceft3inv~ ~cemt1inv~ ~cemt2inv~ ~cemt3inv~ ~chmt1inv~ ~chmt2inv~ ~chmt3inv~ ~cift1inv~ ~cift2inv~ ~cift3inv~ ~cimt1inv~ ~cimt2inv~ ~cimt3inv~ ~chft1inv~ ~chft2inv~ ~chft3inv~ BEGIN

	COPY ~1pp/core/1ppfixed/%paperdoll%.bam~ ~override~

	ACTION_IF FILE_EXISTS_IN_GAME ~%paperdoll%.plt~ BEGIN
		COPY ~1pp/core/ilove.plt~ ~override/%paperdoll%.plt~
	END

END


/* ================================ *
 *  CLONING PLATE ARMOR ANIMATIONS  *
 * ================================ */
// We are duplicating the plate armour animations here to avoid massive crashing

PRINT @110001 // ~Creating T4 duplicates...~
SILENT

ACTION_FOR_EACH category IN ~df~ ~dm~ ~ef~ ~hf~ ~hm~ ~if~ ~im~ BEGIN
	ACTION_FOR_EACH sequence IN ~a1~ ~a2~ ~a3~ ~a4~ ~a5~ ~a6~ ~a7~ ~a8~ ~a9~ ~ca~ ~g1~ ~g11~ ~g12~ ~g13~ ~g14~ ~g15~ ~g16~ ~g17~ ~g18~ ~g19~ ~sa~ ~ss~ ~sx~ BEGIN
		COPY_EXISTING ~c%category%f4%sequence%.bam~ ~override/c%category%t4%sequence%.bam~
	END
END

COPY_EXISTING 	~cemc4a1.bam~	~override/cemt4a1.bam~
				~cemc4a2.bam~	~override/cemt4a2.bam~
				~cemc4a3.bam~	~override/cemt4a3.bam~
				~cemc4a4.bam~	~override/cemt4a4.bam~
				~cemc4a5.bam~	~override/cemt4a5.bam~
				~cemc4a6.bam~	~override/cemt4a6.bam~
				~cemc4a7.bam~	~override/cemt4a7.bam~
				~cemc4a8.bam~	~override/cemt4a8.bam~
				~cemc4a9.bam~	~override/cemt4a9.bam~
				~cemc4ca.bam~	~override/cemt4cA.bam~
				~cemc4g1.bam~	~override/cemt4g1.bam~
				~cemc4g11.bam~	~override/cemt4g11.bam~
				~cemc4g12.bam~	~override/cemt4g12.bam~
				~cemc4g13.bam~	~override/cemt4g13.bam~
				~cemc4g14.bam~	~override/cemt4g14.bam~
				~cemc4g15.bam~	~override/cemt4g15.bam~
				~cemc4g16.bam~	~override/cemt4g16.bam~
				~cemc4g17.bam~	~override/cemt4g17.bam~
				~cemc4g18.bam~	~override/cemt4g18.bam~
				~cemc4g19.bam~	~override/cemt4g19.bam~
				~cemc4sa.bam~	~override/cemt4sa.bam~
				~cemc4ss.bam~	~override/cemt4ss.bam~
				~cemc4sx.bam~	~override/cemt4sx.bam~

ACTION_IF (FILE_EXISTS_IN_GAME ~cdff4inv.bam~) BEGIN
	COPY_EXISTING ~cdff4inv.bam~ ~override/cdft4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~cdmf4inv.bam~) BEGIN
	COPY_EXISTING ~cdmf4inv.bam~ ~override/cdmt4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ceff4inv.bam~) BEGIN
	COPY_EXISTING ~ceff4inv.bam~ ~override/ceft4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~cemf4inv.bam~) BEGIN
	COPY_EXISTING ~cemc4inv.bam~ ~override/cemt4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~chff4inv.bam~) BEGIN
	COPY_EXISTING ~chff4inv.bam~ ~override/chft4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~chmf4inv.bam~) BEGIN
	COPY_EXISTING ~chmf4inv.bam~ ~override/chmt4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ciff4inv.bam~) BEGIN
	COPY_EXISTING ~ciff4inv.bam~ ~override/cift4inv.bam~
END

ACTION_IF (FILE_EXISTS_IN_GAME ~cimf4inv.bam~) BEGIN
	COPY_EXISTING ~cimf4inv.bam~ ~override/cimt4inv.bam~
END

// END of COMPONENT