// Split from main installer
// Work with me here, WeiDU

/*  ===============================================================================================================================================================================  *
 *  change-log:                                                                                                                                                                      *
 *  -----------                                                                                                                                                                      *
 *      - Coding simplification:                                                                                                                                                     *
 *			Avoid copying and overwriting the same files again and again...                                                                                                          *
 *			Group actions or patches with ACTION_FOR_EACH and PATCH_FOR_EACH whenever possible.                                                                                      *
 *                                                                                                                                                                                   *
 *      - Code re-written with new functions to provide automatic process:                                                                                                           *
 *			1. Clears duplicated vanilla #89 and #197 effects in SPL and ITM files (function GW_CLEAR_DUPLICATED_OPCODES).                                                           *
 *	  		2. Checks if protection from SPEAR and/or Bounce SPEAR is already set before adding it (function GW_CLEAR_DUPLICATED_OPCODES).                                           *
 *			3. Function GW_MODIFY_PROJ replaces BWP Fixpack patch (401_projectiles.tph.patch: Modifies the projectile value only when needed (does not patch melee headers anymore). *
 *			   It Replaces ADD_ITEM_EQEFFECT, ADD_ITEM_EFFECT, ADD_SPELL_EFFECT and macros with the newer and more efficient CLONE_EFFECT WeiDU function to give new opcodes #83 and *
 *             #197 the same settings than existing ones (target, resist_dispel, duration, power...). It modifies all extended headers and avoids writing lines of codes!            *
 *			5. Provides partial IR and SR compatibility in a rather softer way.                                                                                                      *
 *                                                                                                                                                                                   *
 *  specific fixes:                                                                                                                                                                  *
 *  ---------------                                                                                                                                                                  *
 *      - #201  ==> ax1h08.itm (Hangard's Axe +2), ax1h09.itm (Rifthome Axe +3), ax1h10.itm (Azuredge +3) & ax1h16.itm (K'logarath +4): add IR compatibility (patches itemB files).  *
 *      - #357  ==> bolt09.itm (Bolt +3): fixes typo (was patching bolt06 instead of bolt09).                                                                                        *
 *      - #465	==> Quivers of Plenty, Cases of Plenty, Bags of Plenty & Sling of Everard: Why quivers and launchers needing no ammo should not get new 1pp projectiles?             *
 *      - #514  ==> dagg11.itm (Boomerang Dagger +2) and dagg12.itm (Firetooth +3): add IR compatibility (patches itemB files).                                                      *
 *      - #629	==> sper04.itm (Javelin): switches Thrown and Melee strings ref in tooltip.2da. Use function GW_ADD_TOOLTIP to add new entries in tooltip.2da.                       *
 *      - #672  ==> spells and items protecting from or bouncing projectiles: add missing protections from new normal 1pp amo projectiles (1axe05, 1dagg05 and 1dart01).             *
 *					The code was only providing protection from 1arow01 and 1bolt01 new projectiles.                                                                                 *
 *      - #714  ==> npshld.itm (Delryn Family Shield): same fix as above + IR compatibility (protection from all types of missiles, including magic ones).                           *
 *      - #746  ==> shld24.itm (Reflection Shield +1): same fix as above + clears duplicate entries and adds IR compatibility (IR replaces opcode #197 with opcode #83).             *
 *      - #769	==>	brac18.itm (Gloves of Missile Snaring): Don't forget to add Protection from projectiles if IR is installed.                                                      *
 *      - #790	==>	spin546.spl (Inertial Barrier), spcl721.spl (Storm Shield) & spcl914.spl (Greater Evasion): patches all headers (was patching only the first header), fixes      *
 *                  wrong duration, target, power, resist dispel... Adds missing protections from new normal 1pp ammo projectiles (1axe05, 1dagg05 and 1dart01). The code was only   *
 *                  providing protection from 1arow01 and 1bolt01 new projectiles.                                                                                                   *
 *      - #813	==> Protection From Normal Missiles spells (spra303, spwi311, & cdwi311 added from BG2 Fixpack): same fixes as above + SR compatibility (protection from all types   *
 *                  of missiles, including magic ones)..                                                                                                                             *
 *      - #846	==> sppr613.spl (Physical Mirror): clear duplicate entries and adds SR compatibility (SR replaces opcode #197 with opcode #83).                                      * 
 *      - #870	==>	Includes Entropy Shield spell (protection from new 1pp projectiles) if IWDIfication is installed.                                                                *
 *  ===============================================================================================================================================================================  */

// ----------------------------- //
//    ITEMS                line  //
// ----------------------------- //
//  ARROWS               #   66  //
//  AXES                 #  180  //
//  BOLTS                #  246  //
//  BULLETS              #  372  //
//  DAGGERS              #  494  //
//  DARTS                #  556  //
//  JAVELIN              #  629  //
//  PATCH SPELLS & ITEMS #  669  //
// ----------------------------- //

PRINT @401000 /* Installing */

LAM ~Locations~

// Gwen fix: removes SPER04.ITM from the list to avoid overwriting the file.
ACTION_FOR_EACH resource IN ~ARARROW.BAM~ ~ARROWEX.PRO~ ~ARROWFLB.PRO~ ~ARROWFLG.PRO~ ~ARROWFLI.PRO~ ~ARROWFLM.PRO~ ~AXE.BAM~ ~AXE.PRO~ ~AXEEX.PRO~ ~BOLT.BAM~ ~BOLT.PRO~ ~BOLTEX.PRO~ ~DAGGER.BAM~ ~DART.BAM~ ~DART.PRO~ ~DARTEX.PRO~ ~MAGICSTN.BAM~ ~SPEAR.BAM~ ~SPEAR.PRO~ ~SPEAREX.PRO~ ~SPFLMARR.BAM~ ~ISPER04.BAM~ BEGIN

	COPY ~1pp/additions/projectiles_1pp/%resource%~ ~override~

END


/*  ======================  *
 *          ARROWS          *
 *  ======================  */

ACTION_FOR_EACH 1pp_proj IN 1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 BEGIN	// 1pp new arrows projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END
//TODO : VERIF CLEAR_IDS_MAP
// Arrow
ACTION_FOR_EACH arrow IN ~AROW01~ ~_AROW01~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROWHVY" new_proj = "1arow01" END
			END
		BUT_ONLY
	END
END

// Arrow +1
ACTION_FOR_EACH arrow IN ~AROW02~ ~_AROW02~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROWHVY" new_proj = "1arow02" END
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow02" END	// Gwen fix: IR compatibility
			END
		BUT_ONLY
	END
END


// Arrow of Slaying
ACTION_FOR_EACH arrow IN ~AROW03~ ~_AROW03~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow03" END
			END
		BUT_ONLY
	END
END

// Acid Arrow
ACTION_FOR_EACH arrow IN ~AROW04~ ~_AROW04~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow04" END
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROWFLG" new_proj = "1arow04" END	// Gwen fix: IR compatibility
			END
		BUT_ONLY
	END
END

// Arrow of Biting
ACTION_FOR_EACH arrow IN ~AROW05~ ~_AROW05~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow05" END
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROWHVY" new_proj = "1arow05" END	// Gwen fix: IR compatibility
			END
		BUT_ONLY
	END
END

// Arrow of Dispelling
ACTION_FOR_EACH arrow IN ~AROW07~ ~_AROW07~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow07" END
			END
		BUT_ONLY
	END
END

// Arrow of Piercing
ACTION_FOR_EACH arrow IN ~AROW10~ ~_AROW10~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow10" END
			END
		BUT_ONLY
	END
END

// Arrow +2
ACTION_FOR_EACH arrow IN ~AROW11~ ~_AROW11~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow11" END
			END
		BUT_ONLY
	END
END

// Arrow +3
ACTION_FOR_EACH arrow IN ~AROW15~ ~_AROW15~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%arrow%.ITM~) THEN BEGIN
		COPY_EXISTING ~%arrow%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "ARROW" new_proj = "1arow15" END
			END
		BUT_ONLY
	END
END


/*  ======================  *
 *           AXES           *
 *  ======================  */

ACTION_FOR_EACH 1pp_proj IN 1axe05 1axe08 1axe09 1axe10 1axe16 BEGIN	// 1pp new axes projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END

// Throwing Axe +2
ACTION_FOR_EACH axe IN ~AX1H05~ ~_AX1H05~ ~AX1H06~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%axe%.ITM~) THEN BEGIN
		COPY_EXISTING ~%axe%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "AXE" new_proj = "1axe05" END
			END
		BUT_ONLY
	END
END

// Hangard's Axe +2
ACTION_FOR_EACH axe IN ~AX1H08~ ~AX1H08B~ BEGIN			// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%axe%.ITM~) THEN BEGIN
		COPY_EXISTING ~%axe%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "AXE" new_proj = "1axe08" END
			END
		BUT_ONLY
	END
END

// Rifthome Axe +3
ACTION_FOR_EACH axe IN ~AX1H09~ ~AX1H09B~ BEGIN			// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%axe%.ITM~) THEN BEGIN
		COPY_EXISTING ~%axe%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "AXE" new_proj = "1axe09" END
			END
		BUT_ONLY
	END
END

// Azuredge +3
ACTION_FOR_EACH axe IN ~AX1H10~ ~AX1H10B~ BEGIN			// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%axe%.ITM~) THEN BEGIN
		COPY_EXISTING ~%axe%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "AXE" new_proj = "1axe10" END
			END
		BUT_ONLY
	END
END

// K'logarath +4
ACTION_FOR_EACH axe IN ~AX1H16~ ~AX1H16B~ BEGIN			// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%axe%.ITM~) THEN BEGIN
		COPY_EXISTING ~%axe%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "AXE" new_proj = "1axe16" END
			END
		BUT_ONLY
	END
END


/*  =======================  *
 *           BOLTS           *
 *  =======================  */

ACTION_FOR_EACH 1pp_proj IN 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 BEGIN	// 1pp new bolts projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END

// Bolt
ACTION_FOR_EACH bolt IN ~BOLT01~ ~_BOLT01~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 27						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt01" END
			END
		BUT_ONLY
	END
END

// Bolt +1
ACTION_FOR_EACH bolt IN ~BOLT02~ ~_BOLT02~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 90						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt02" END
			END
		BUT_ONLY
	END
END

// Bolt of Lightning
ACTION_FOR_EACH bolt IN ~BOLT03~ ~_BOLT03~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 218						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt03" END
			END
		BUT_ONLY
	END
END

// Bolt of Biting
ACTION_FOR_EACH bolt IN ~BOLT04~ ~_BOLT04~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 252						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt04" END
			END
		BUT_ONLY
	END
END

// Bolt of Polymorphing
ACTION_FOR_EACH bolt IN ~BOLT05~ ~_BOLT05~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 227						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt05" END
			END
		BUT_ONLY
	END
END

// Bolt +2
ACTION_FOR_EACH bolt IN ~BOLT06~ ~_BOLT06~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bolt%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bolt%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 81						// colour index
				SET location = wpink					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt06" END
			END
		BUT_ONLY
	END
END

// Blessed Bolt
ACTION_IF (FILE_EXISTS_IN_GAME ~BOLT08.ITM~) THEN BEGIN
	COPY_EXISTING ~BOLT08.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPM ~clear~
			SET gradient = 208						// colour index
			SET location = wpink					// location
			LPM ~colour~
		END
	BUT_ONLY
END

// Bolt +3: fixes typo (was patching bolt06 instead of bolt09).
ACTION_IF (FILE_EXISTS_IN_GAME ~BOLT09.ITM~) THEN BEGIN
	COPY_EXISTING ~BOLT09.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPM ~clear~
			SET gradient = 186						// colour index
			SET location = wpink					// location
			LPM ~colour~
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BOLT" new_proj = "1bolt09" END
		END
	BUT_ONLY
END


/*  =========================  *
 *           BULLETS           *
 *  =========================  */

ACTION_FOR_EACH 1pp_proj IN 1bull02 1bull03 1bull04 1bull05 1bull06 BEGIN	// 1pp new bullets projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END

// Bullet
ACTION_FOR_EACH bullet IN ~BULL01~ ~_BULL01~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bullet%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bullet%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 27						// colour index
				SET location = wgrey					// location
				LPM ~colour~
			END
		BUT_ONLY
	END
END

// Bullet +1
ACTION_FOR_EACH bullet IN ~BULL02~ ~_BULL02~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bullet%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bullet%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 218						// colour index
				SET location = wgrey					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BULLET" new_proj = "1bull02" END
			END
		BUT_ONLY
	END
END

// Bullet +2
ACTION_FOR_EACH bullet IN ~BULL03~ ~_BULL03~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%bullet%.ITM~) THEN BEGIN
		COPY_EXISTING ~%bullet%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPM ~clear~
				SET gradient = 70						// colour index
				SET location = wgrey					// location
				LPM ~colour~
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BULLET" new_proj = "1bull03" END
			END
		BUT_ONLY
	END
END

// Sunstone Bullet +1
ACTION_IF (FILE_EXISTS_IN_GAME ~BULL04.ITM~) THEN BEGIN
	COPY_EXISTING ~BULL04.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPM ~clear~
			SET gradient = 115						// colour index
			SET location = wgrey					// location
			LPM ~colour~
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BULLET" new_proj = "1bull04" END
		END
	BUT_ONLY
END

// Bullet +3
ACTION_IF (FILE_EXISTS_IN_GAME ~BULL05.ITM~) THEN BEGIN
	COPY_EXISTING ~BULL05.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPM ~clear~
			SET gradient = 148						// colour index
			SET location = wgrey					// location
			LPM ~colour~
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BULLET" new_proj = "1bull05" END
		END
	BUT_ONLY
END

// Bullet +4
ACTION_IF (FILE_EXISTS_IN_GAME ~BULL06.ITM~) THEN BEGIN
	COPY_EXISTING ~BULL06.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPM ~clear~
			SET gradient = 19						// colour index
			SET location = wgrey					// location
			LPM ~colour~
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "BULLET" new_proj = "1bull06" END
		END
	BUT_ONLY
END


// Gwen fix: Why quivers and launchers needing no ammo should not get new 1pp projectiles?

// BUILDING ARRAY
ACTION_CLEAR_ARRAY 1pp_proj_quivers
ACTION_DEFINE_ASSOCIATIVE_ARRAY 1pp_proj_quivers BEGIN
	QUIVER01,	ARROW	=>	1arow02	// Quiver of Plenty +1					-->	set arrow +1 projectile.
	QUIVER02,	BOLT	=>	1bolt02	// Case of Plenty +1					-->	set bolt +1 projectile.
	QUIVER03,	ARROW	=>	1arow11	// Quiver of Plenty +2					-->	set arrow +2 projectile.
	QUIVER04,	BOLT	=>	1bolt06	// Case of Plenty +2					-->	set bolt +2 projectile.
	QUIVER05,	BULLET	=>	1bull02	// Bag of Plenty +1						-->	set bullet +1 projectile.
	QUIVER06,	BULLET	=>	1bull03	// Bag of Plenty +2						-->	set bullet +2 projectile.
	WASLING,	BULLET	=>	1bull03	// Sling of Everard +5 (THAC0 +5, +2)	-->	set bullet +2 projectile.
END

ACTION_PHP_EACH 1pp_proj_quivers AS quiver => proj BEGIN

	ACTION_IF (FILE_EXISTS_IN_GAME ~%quiver%.ITM~) THEN BEGIN
		COPY_EXISTING ~%quiver%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = EVAL "%quiver_1%" new_proj = EVAL "%proj%" END
			END
		BUT_ONLY
	END

END
ACTION_CLEAR_ARRAY 1pp_proj_quivers


/*  =========================  *
 *           DAGGERS           *
 *  =========================  */

ACTION_FOR_EACH 1pp_proj IN 1dagg05 1dagg11 1dagg12 1dagg16 BEGIN	// 1pp new daggers projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END

// Throwing Dagger
ACTION_FOR_EACH dagger IN ~DAGG05~ ~_DAGG05~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dagger%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dagger%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DAGGER" new_proj = "1dagg05" END
			END
		BUT_ONLY
	END
END

// Boomerang Dagger +2
ACTION_FOR_EACH dagger IN ~DAGG11~ ~DAGG11B~ BEGIN		// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dagger%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dagger%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DAGGER" new_proj = "1dagg11" END
			END
		BUT_ONLY
	END
END

// Firetooth +3
ACTION_FOR_EACH dagger IN ~DAGG12~ ~DAGG12B~ BEGIN		// Gwen fix: add IR compatibility
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dagger%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dagger%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DAGGER" new_proj = "1dagg12" END
			END
		BUT_ONLY
	END
END

// Poisoned Throwing Dagger
ACTION_IF (FILE_EXISTS_IN_GAME ~DAGG16.ITM~) THEN BEGIN
	COPY_EXISTING ~DAGG16.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DAGGER" new_proj = "1dagg16" END
		END
	BUT_ONLY
END

// Returning Throwing Dagger +1 (Mod Song and Silence)
ACTION_IF (FILE_EXISTS_IN_GAME ~a!thtd.ITM~) THEN BEGIN
	COPY_EXISTING ~a!thtd.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DAGGER" new_proj = "1dagg16" END
		END
	BUT_ONLY
END


/*  =======================  *
 *           DARTS           *
 *  =======================  */

ACTION_FOR_EACH 1pp_proj IN 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN	// 1pp new darts projectiles

	ADD_PROJECTILE ~1pp/additions/projectiles_1pp/%1pp_proj%.pro~

END

// Dart
ACTION_FOR_EACH dart IN ~DART01~ ~_DART01~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dart%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dart%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart01" END
			END
		BUT_ONLY
	END
END

// Dart +1
ACTION_FOR_EACH dart IN ~DART02~ ~_DART02~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dart%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dart%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart02" END
			END
		BUT_ONLY
	END
END

// Dart of Stunning
ACTION_FOR_EACH dart IN ~DART03~ ~_DART03~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dart%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dart%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart03" END
			END
		BUT_ONLY
	END
END

// Dart of Wounding
ACTION_FOR_EACH dart IN ~DART04~ ~_DART04~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%dart%.ITM~) THEN BEGIN
		COPY_EXISTING ~%dart%.ITM~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
				LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart04" END
			END
		BUT_ONLY
	END
END

// Asp's Nest
ACTION_IF (FILE_EXISTS_IN_GAME ~DART05.ITM~) THEN BEGIN
	COPY_EXISTING ~DART05.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart05" END
		END
	BUT_ONLY
END

// Crimson Dart +3
ACTION_IF (FILE_EXISTS_IN_GAME ~DART08.ITM~) THEN BEGIN
	COPY_EXISTING ~DART08.ITM~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) THEN BEGIN	// protects against invalid files
			LPF ~GW_MODIFY_PROJ~ STR_VAR old_proj = "DART" new_proj = "1dart08" END
		END
	BUT_ONLY
END


/*  =========================  *
 *           JAVELIN           *
 *  =========================  */

// Javelin
COPY ~1pp\additions\projectiles_1pp/SPER04.ITM~ ~override~
	SAY NAME1 @401101 /* Javelin */
	SAY NAME2 @401101
	SAY UNIDENTIFIED_DESC @401102 /* The javelin is a type of light spear designed for throwing, usually with a narrower tip. While this makes it less resilient and powerful in close quarter combat, it can be a deadly weapon if the advantage of its range is used to its fullest extent.

STATISTICS:

Damage: 1D6
Damage type: piercing
Weight: 5
Speed Factor: 6
Proficiency Type: Spear
Type: 2-handed
Requires: 5 Strength
Not Usable By:
 Cleric
 Mage
 Thief */

// Gwen fix: switch Thrown and Melee strings ref in tooltip.2da.
LAF ~GW_ADD_TOOLTIP~ STR_VAR GW_item_tooltip0 = "401104" GW_item_tooltip1 = "401103" GW_objet = "SPER04" END	// Thrown, Melee

// Cutpurse (Bridge)
COPY_EXISTING ~bshop01.sto~ ~override~
	ADD_STORE_ITEM ~sper04~   AFTER  ~dart01~ #10 #0 #0 ~IDENTIFIED~ #10

// Peddler (Trademeet)
COPY_EXISTING ~trmer01.sto~ ~override~
	ADD_STORE_ITEM ~sper04~   AFTER  ~dart01~ #10 #0 #0 ~IDENTIFIED~ #5

// Maheer (Waukeen's Promenade)
COPY_EXISTING ~shop03.sto~ ~override~
	ADD_STORE_ITEM ~sper04~   AFTER  ~sper02~ #10 #0 #0 ~IDENTIFIED~ #10


/*  =================================================================================================  *
 *           ADD 1pp PROJECTILES TO SPELLS AND ITEMS PROTECTING FROM OR BOUNCING PROJECTILES           *
 *  =================================================================================================  */

// Gwen fix: add missing protections from new normal 1pp amo projectiles (1axe05, 1dagg05 and 1dart01). The code was only providing protection from 1arow01 and 1bolt01 new projectiles.
ACTION_FOR_EACH item IN ~AURSTAF~ ~DRAGRING~ ~MAGEBRAC~ ~SLAYERWP~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%item%.ITM~) THEN BEGIN
		COPY_EXISTING ~%item%.ITM~ ~override~
			LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
			PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
				PATCH_FOR_EACH proj IN 1arow01 1bolt01 1axe05 1dagg05 1dart01 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
				END
			END
		BUT_ONLY
	END
END

// Blue Globe: same fix as above
ACTION_IF (FILE_EXISTS_IN_GAME ~GLOBBLU4.ITM~) THEN BEGIN
	COPY_EXISTING ~GLOBBLU4.ITM~ ~override~
		LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
		PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
			LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
			PATCH_FOR_EACH proj IN 1arow01 1bolt01 1axe05 1dagg05 1dart01 BEGIN
				SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
				PATCH_IF projnb > 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
				END
			END
			// Special case: SPEAR (55) needs a special treatment.
			PATCH_IF GW_param2_55 = 0 BEGIN
				LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
			END
		END
	BUT_ONLY
END

// Delryn Family Shield
//Gwen fix: same fix as above + adds IR compatibility (protection from all types of missiles, including magic ones).
ACTION_IF (FILE_EXISTS_IN_GAME ~NPSHLD.ITM~) THEN BEGIN
	COPY_EXISTING ~NPSHLD.ITM~ ~override~
		LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
		PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN		// protects against invalid files
			LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
			PATCH_IF IR_INSTALLED BEGIN
				PATCH_FOR_EACH proj IN						// 1pp new projectiles
					1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
					END
				END
			END ELSE BEGIN
				PATCH_FOR_EACH proj IN 1arow01 1bolt01 1axe05 1dagg05 1dart01 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
					END
				END
			END
			// Special case: SPEAR (55) needs a special traitment.
			PATCH_IF GW_param2_55 = 0 BEGIN
				LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
			END
		END
	BUT_ONLY
END

// Reflection Shield +1
// Gwen fix: clears duplicate entries and adds IR compatibility (IR replaces opcode #197 with opcode #83).
ACTION_IF (FILE_EXISTS_IN_GAME ~shld24.ITM~) THEN BEGIN
	COPY_EXISTING ~shld24.ITM~ ~override~
		LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
		PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
			PATCH_FOR_EACH GW_opcode IN 83 197 BEGIN
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = GW_opcode GW_effects_type = 2 RET GW_param2_55 END
				PATCH_FOR_EACH proj IN					// 1pp new projectiles
					1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = GW_opcode parameter2 = projnb STR_VAR insert = last END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = GW_opcode parameter2 = 55 STR_VAR insert = last END
				END
			END
		END
	BUT_ONLY
END

// Gwen fix: Don't forget Gloves of Missile Snaring if IR is installed!
ACTION_IF (FILE_EXISTS_IN_GAME ~brac18.ITM~) THEN BEGIN
	COPY_EXISTING ~brac18.ITM~ ~override~
		LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
		PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
			LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
			PATCH_FOR_EACH proj IN						// 1pp new projectiles
				1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
				SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
				PATCH_IF projnb > 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
				END
			END
			// Special case: SPEAR (55) needs a special treatment.
			PATCH_IF GW_param2_55 = 0 BEGIN
				LPF CLONE_EFFECT INT_VAR silent = 1 check_headers = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
			END
		END
	BUT_ONLY
END

// Inertial Barrier - Storm Shield - Greater Evasion
// Gwen fix: patches all headers (was patching only the first header), fixes wrong duration, target, power, resist dispel... Adds missing protections from new normal 1pp ammo projectiles (1axe05, 1dagg05 and 1dart01). The code was only providing protection from 1arow01 and 1bolt01 new projectiles.
ACTION_FOR_EACH spell IN ~SPIN546~ ~SPCL721~ ~SPCL914~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.SPL~) THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
			LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
			PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
				PATCH_FOR_EACH proj IN 1arow01 1bolt01 1axe05 1dagg05 1dart01 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
				END
			END
		BUT_ONLY
	END
END

// Protection From Normal Missiles (adds CDWI311 from BG2 Fixpack)
//Gwen fix: same as above + add SR compatibility (protection from all types of missiles, including magic ones).
ACTION_FOR_EACH spell IN ~SPRA303~ ~SPWI311~ ~CDWI311~ BEGIN
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.SPL~) THEN BEGIN
		COPY_EXISTING ~%spell%.SPL~ ~override~
			LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
			PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 2 RET GW_param2_55 END
				PATCH_IF SR_INSTALLED BEGIN
					PATCH_FOR_EACH proj IN					// 1pp new projectiles
						1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
						SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
						PATCH_IF projnb > 0 BEGIN
							LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
						END
					END
				END ELSE BEGIN
					PATCH_FOR_EACH proj IN 1arow01 1bolt01 1axe05 1dagg05 1dart01 BEGIN
						SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
						PATCH_IF projnb > 0 BEGIN
							LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = last END
						END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = last END
				END
			END
		BUT_ONLY
	END
END

// Physical Mirror
// Gwen fix: clears duplicate entries and adds SR compatibility (SR replaces opcode #197 with opcode #83).
ACTION_IF (FILE_EXISTS_IN_GAME ~sppr613.spl~) THEN BEGIN
	COPY_EXISTING ~sppr613.spl~ ~override~
		LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
		PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
			PATCH_FOR_EACH GW_opcode IN 83 197 BEGIN
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = GW_opcode GW_effects_type = 2 RET GW_param2_55 END
				PATCH_FOR_EACH proj IN					// 1pp new projectiles
					1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = GW_opcode parameter2 = projnb STR_VAR insert = last END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = GW_opcode parameter2 = 55 STR_VAR insert = last END
				END
			END
		END
	BUT_ONLY
END

// Gwen fix: Don't forget Entropy Shield spell if IWDIfication is installed!
ACTION_IF (FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_ENTROPY_SHIELD~)) BEGIN
	LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = CLERIC_ENTROPY_SHIELD RET spell_num spell_res END
	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell_res%.SPL~) THEN BEGIN
		COPY_EXISTING ~%spell_res%.SPL~ ~override~
			LPF ~GW_DEF_OFFSETS_FILE~ RET GW_oe GW_oc GW_al GW_fx GW_fi GW_fc GW_min_size END
			PATCH_IF (SOURCE_SIZE > GW_min_size) THEN BEGIN	// protects against invalid files
				LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 0 RET GW_param2_55 END
				PATCH_FOR_EACH proj IN						// 1pp new projectiles
					1arow01 1arow02 1arow03 1arow04 1arow05 1arow07 1arow10 1arow11 1arow15 1axe05 1axe08 1axe09 1axe10 1axe16 1bolt01 1bolt02 1bolt03 1bolt04 1bolt05 1bolt06 1bolt09 1bull02 1bull03 1bull04 1bull05 1bull06 1dagg05 1dagg11 1dagg12 1dagg16 1dart01 1dart02 1dart03 1dart04 1dart05 1dart08 BEGIN
					SET projnb = IDS_OF_SYMBOL (~projectl~ ~%proj%~)
					PATCH_IF projnb > 0 BEGIN
						LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = projnb STR_VAR insert = above END
					END
				END
				// Special case: SPEAR (55) needs a special treatment.
				PATCH_IF GW_param2_55 = 0 BEGIN
					LPF CLONE_EFFECT INT_VAR silent = 1 check_globals = 0 multi_match = 1 match_opcode = 83 parameter2 = 55 STR_VAR insert = above END
				END
			END
			LPF ~GW_CLEAR_DUPLICATED_OPCODES~ INT_VAR GW_opcode_to_check = 83 GW_effects_type = 0 END	// Gwen fix: workaround to avoid duplicating opcode #83 effects according to IWDification install order.
		BUT_ONLY
	END
END

//END of COMPONENT