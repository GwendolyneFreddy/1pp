/* ================================================================================================================================================================================ *
 *  v 4.2.0                                                                                                                                                                  O n e  *
 *                                           C O M P O N E N T   :   A d d i t i o n a l   S h i e l d   A n i m a t i o n s   (c o r e)                                            *
 *                                                                                                                                                                                  *
 * ================================================================================================================================================================================ *
 *  This component includes updated versions of Baldur's Gate II's shield animations, as well as several completely new animation types. Also included is one variant that shares   *
 *  common animations but has distinct paperdolls.                                                                                                                                  *
 * ================================================================================================================================================================================ *
 *  change-log:                                                                                                                                                                     *
 *  -----------                                                                                                                                                                     *
 *      - Coding simplification:                                                                                                                                                    *
 *          Copied entire folders instead of infinite lines and group actions or patches with ACTION_FOR_EACH and PATCH_FOR_EACH whenever possible.                                 *
 * ================================================================================================================================================================================ */

PRINT @206000 // ~Copying files...~
SILENT

COPY ~1pp/additions/obj_anim/c0~ ~override~
COPY ~1pp/additions/obj_anim/c13~ ~override~
COPY ~1pp/additions/obj_anim/c2~ ~override~
COPY ~1pp/additions/obj_anim/c4~ ~override~
COPY ~1pp/additions/obj_anim/c5~ ~override~
COPY ~1pp/additions/obj_anim/c6~ ~override~
COPY ~1pp/additions/obj_anim/c7~ ~override~


PRINT @206001 // ~Expanding C13...~
SILENT

ACTION_FOR_EACH category IN ~l~ ~m~ ~n~ ~s~ BEGIN
	ACTION_FOR_EACH sequence IN ~a1~ ~a3~ ~a5~ ~g1~ ~ss~ BEGIN
		COPY ~1pp/additions/obj_anim/c13/wq%category%c1%sequence%.bam~ ~override/wq%category%c3%sequence%.bam~
	END
END

PRINT @206002 // ~Building BG1 animation compatibility...~
SILENT

ACTION_FOR_EACH set IN ~c0~ ~c5~ BEGIN
	ACTION_FOR_EACH category IN ~l~ ~m~ ~s~ BEGIN
		ACTION_FOR_EACH sequence IN ~a1~ ~a1e~ ~a3~ ~a3e~ ~a5~ ~a5e~ ~g1~ ~g1e~ ~w2~ ~w2e~ BEGIN
			COPY_EXISTING ~wp%category%d2%sequence%.bam~ ~override/wp%category%%set%%sequence%.bam~
		END
	END
END

ACTION_FOR_EACH set IN ~c1~ ~c3~ ~c7~ BEGIN
	ACTION_FOR_EACH category IN ~l~ ~m~ ~s~ BEGIN
		ACTION_FOR_EACH sequence IN ~a1~ ~a1e~ ~a3~ ~a3e~ ~a5~ ~a5e~ ~g1~ ~g1e~ ~w2~ ~w2e~ BEGIN
			COPY_EXISTING ~wp%category%d3%sequence%.bam~ ~override/wp%category%%set%%sequence%.bam~
		END
	END
END

ACTION_FOR_EACH set IN ~c2~ ~c6~ BEGIN
	ACTION_FOR_EACH category IN ~l~ ~m~ ~s~ BEGIN
		ACTION_FOR_EACH sequence IN ~a1~ ~a1e~ ~a3~ ~a3e~ ~a5~ ~a5e~ ~g1~ ~g1e~ ~w2~ ~w2e~ BEGIN
			COPY_EXISTING ~wp%category%d4%sequence%.bam~ ~override/wp%category%%set%%sequence%.bam~
		END
	END
END

ACTION_FOR_EACH category IN ~l~ ~m~ ~s~ BEGIN
	ACTION_FOR_EACH sequence IN ~a1~ ~a1e~ ~a3~ ~a3e~ ~a5~ ~a5e~ ~g1~ ~g1e~ ~w2~ ~w2e~ BEGIN
		COPY_EXISTING ~wp%category%d1%sequence%.bam~ ~override/wp%category%c4%sequence%.bam~
	END
END

ACTION_IF (is_bg2 OR is_tob OR is_tutu OR is_iwd) BEGIN // (GAME_IS ~totlm how bg2 tob tutu tutu_totsc ca~)
	PRINT @206003 // ~Installing paperdoll images...~
	SILENT

	ACTION_FOR_EACH paperdoll IN ~wphc0inv~ ~wphc1inv~ ~wphc2inv~ ~wphc3inv~ ~wphc4inv~ ~wphc5inv~ ~wphc6inv~ ~wphc7inv~ ~wplc0inv~ ~wplc1inv~ ~wplc2inv~ ~wplc3inv~ ~wplc4inv~ ~wplc5inv~ ~wplc6inv~ ~wplc7inv~ ~wpmc0inv~ ~wpmc1inv~ ~wpmc2inv~ ~wpmc3inv~ ~wpmc4inv~ ~wpmc5inv~ ~wpmc6inv~ ~wpmc7inv~ ~wpnc0inv~ ~wpnc1inv~ ~wpnc2inv~ ~wpnc3inv~ ~wpnc4inv~ ~wpnc5inv~ ~wpnc6inv~ ~wpnc7inv~ ~wpsc0inv~ ~wpsc1inv~ ~wpsc2inv~ ~wpsc3inv~ ~wpsc4inv~ ~wpsc5inv~ ~wpsc6inv~ ~wpsc7inv~ BEGIN
		COPY ~1pp/additions/obj_1pp/%paperdoll%.bam~ ~override~
	END

END

// END of COMPONENT