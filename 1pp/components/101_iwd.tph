/* =================================================================================================================================================================================== *
 *                                                                                                                                                                                     *
 *                                                           C O R E   C O M P O N E N T   :   C o r e   p a p e r d o l l s                                                           *
 *                                                                                                                                                                                     *
 * =================================================================================================================================================================================== *
 *  This component ports Baldur's Gate I style paperdolls for the IWD engine.                                                                                                          *
 *  This part installs paperdolls for IWD base and HoW installs and includes paperdolls for otherwise still pristine games.                                                            *
 * =================================================================================================================================================================================== *
 *  change-log:                                                                                                                                                                        *
 *  -----------                                                                                                                                                                        *
 *      - Coding simplification:                                                                                                                                                       *
 *  		Copy entire folders instead of infinite lines (COPY ~1pp/folder/file.ext~ ~override~ or ACTION_FOR_EACH file IN list BEGIN COPY ~1pp/folder/file.ext~ ~override~ END).     *
            Reorganized folder architecture tree: created sub-folders to sort paperdolls .BAM files according to the creature types.                                                   *
 * =================================================================================================================================================================================== */

/* ======= *
 *  TotLM  *
 * ======= */
ACTION_IF (GAME_IS ~totlm~) BEGIN

	OUTER_SET fail = 0

	PRINT @101400 // ~Fixing the train wreck that is half-BG1-half-BG2 paperdoll calls - check.~
	SILENT

	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ || ~%WEIDU_OS%~ STRING_EQUAL_CASE ~unix~) BEGIN	// don't have mac info yet
		COPY ~IDmain.exe~ ~IDmain.exe~
			// determine which version of the game is used, and set the base offset accordingly
			PATCH_IF (SOURCE_SIZE == 6873088) BEGIN	// IWD 1.4.2
				READ_BYTE 0x3d5c2b tst_val
				PATCH_IF (tst_val = 0x6c) BEGIN
					WRITE_BYTE 0x3d5c2b 0x70
					WRITE_BYTE 0x3d5c41 0x70
					WRITE_BYTE 0x3d5fdb 0x70
					WRITE_BYTE 0x3d5ff1 0x70
					WRITE_BYTE 0x3d63ae 0x70
					WRITE_BYTE 0x3d63c4 0x70
					WRITE_BYTE 0x3d66e9 0x70
					WRITE_BYTE 0x3d66ff 0x70
				END ELSE BEGIN
					PATCH_PRINT @101401 // ~Error: could not determine which version of IWD you are using. Please double check that you installed TotLM (v1.4.2).~
					SET fail = 1
				END
			END
	END ELSE BEGIN
		OUTER_SET fail = 1
	END

	ACTION_IF (fail == 1) BEGIN
		PRINT @101402 // ~Could not patch idmain successfully, skipping.~
	END

END


/* ===== *
 *  HoW  *
 * ===== */
ACTION_IF (GAME_IS ~how~) BEGIN

	OUTER_SET fail = 0

	PRINT @101400 // ~Fixing the train wreck that is half-BG1-half-BG2 paperdoll calls - check.~
	SILENT

	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL_CASE ~win32~ || ~%WEIDU_OS%~ STRING_EQUAL_CASE ~unix~) BEGIN // don't have mac info yet
		COPY ~IDmain.exe~ ~IDmain.exe~
			// determine which version of the game is used, and set the base offset accordingly
			PATCH_IF (SOURCE_SIZE == 6840320) BEGIN // IWD 1.4.1
				READ_BYTE 0x3cee1d tst_val
				PATCH_IF (tst_val = 0xc8) BEGIN
					WRITE_BYTE 0x3cee1d 0xcc
					WRITE_BYTE 0x3cee33 0xcc
					WRITE_BYTE 0x3cf1cd 0xcc
					WRITE_BYTE 0x3cf1e3 0xcc
					WRITE_BYTE 0x3cf5a0 0xcc
					WRITE_BYTE 0x3cf5b6 0xcc
					WRITE_BYTE 0x3cf8db 0xcc
					WRITE_BYTE 0x3cf8f1 0xcc
				END ELSE BEGIN
					PATCH_PRINT @101403 // ~Error: could not determine which version of IWD you are using. Please double check that you installed HoW (v1.4.1).~
					SET fail = 1
				END
			END
	END ELSE BEGIN
		OUTER_SET fail = 1
	END

	ACTION_IF (fail == 1) BEGIN
		PRINT @101402 // ~Could not patch idmain successfully, skipping.~
	END

END


/*DEPRECATED as of v4.2.0
ACTION_FOR_EACH paperdoll IN ~cdfc1inv~ ~cdfc2inv~ ~cdfc3inv~ ~cdfc4inv~ ~cdff1inv~ ~cdff2inv~ ~cdff3inv~ ~cdff4inv~ ~cdft1inv~ ~cdft2inv~ ~cdfw1inv~ ~cdfw2inv~ ~cdfw3inv~ ~cdfw4inv~ ~cdmc1inv~ ~cdmc2inv~ ~cdmc3inv~ ~cdmc4inv~ ~cdmf1inv~ ~cdmf2inv~ ~cdmf3inv~ ~cdmf4inv~ ~cdmt1inv~ ~cdmt2inv~ ~cdmw1inv~ ~cdmw2inv~ ~cdmw3inv~ ~cdmw4inv~ ~cefc1inv~ ~cefc2inv~ ~cefc3inv~ ~cefc4inv~ ~ceff1inv~ ~ceff2inv~ ~ceff3inv~ ~ceff4inv~ ~ceft1inv~ ~ceft2inv~ ~cefw1inv~ ~cefw2inv~ ~cefw3inv~ ~cefw4inv~ ~cemc1inv~ ~cemc2inv~ ~cemc3inv~ ~cemc4inv~ ~cemf1inv~ ~cemf2inv~ ~cemf3inv~ ~cemf4inv~ ~cemt1inv~ ~cemt2inv~ ~cemw1inv~ ~cemw2inv~ ~cemw3inv~ ~cemw4inv~ ~chfc1inv~ ~chfc2inv~ ~chfc3inv~ ~chfc4inv~ ~chff1inv~ ~chff2inv~ ~chff3inv~ ~chff4inv~ ~chfm1inv~ ~chft1inv~ ~chft2inv~ ~chfw1inv~ ~chfw2inv~ ~chfw3inv~ ~chfw4inv~ ~chmc1inv~ ~chmc2inv~ ~chmc3inv~ ~chmc4inv~ ~chmf1inv~ ~chmf2inv~ ~chmf3inv~ ~chmf4inv~ ~chmm1inv~ ~chmt1inv~ ~chmt2inv~ ~chmw1inv~ ~chmw2inv~ ~chmw3inv~ ~chmw4inv~ ~cifc1inv~ ~cifc2inv~ ~cifc3inv~ ~cifc4inv~ ~ciff1inv~ ~ciff2inv~ ~ciff3inv~ ~ciff4inv~ ~cift1inv~ ~cift2inv~ ~cimc1inv~ ~cimc2inv~ ~cimc3inv~ ~cimc4inv~ ~cimf1inv~ ~cimf2inv~ ~cimf3inv~ ~cimf4inv~ ~cimt1inv~ ~cimt2inv~ 
~CHFC1INV~ ~CHFC2INV~ ~CHFC3INV~ ~CHFC4INV~ ~CHFF1INV~ ~CHFF2INV~ ~CHFF3INV~ ~CHFF4INV~ ~CHFT1INV~ ~CHFT2INV~ ~CHFW1INV~ ~CHFW2INV~ ~CHFW3INV~ ~CHFW4INV~ BEGIN

	COPY ~1pp/core/base/%paperdoll%.bam~ ~override~

	ACTION_IF FILE_EXISTS_IN_GAME ~%paperdoll%.plt~ BEGIN
		COPY ~1pp/core/ilove.plt~ ~override/%paperdoll%.plt~
	END

END*/
SILENT
ACTION_FOR_EACH paperdoll IN ~dwa_f~ ~dwa_m~ ~elf_f~ ~elf_m~ ~hal_f~ ~hal_m~ ~hum_f~ ~hum_m~ BEGIN

	ACTION_BASH_FOR ~1pp/core/base/%paperdoll%/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		ACTION_IF FILE_EXISTS_IN_GAME ~%BASH_FOR_RES%.plt~ BEGIN
			COPY ~1pp/core/ilove.plt~ ~override/%BASH_FOR_RES%.plt~
		END
	END

END

// END of COMPONENT