// varied 'Armour' visual effects
// IWD style Cause Wounds spells

/*  ==============================================================================================================================================================================  *
 *  change-log:                                                                                                                                                                     *
 *  -----------                                                                                                                                                                     *
 *      - BWP Fixpack patch for individualised armor effects {202_spelltweaks.tph.patch): modifies opcode #215 visual effect in ALL spell headers.                                  *
 *      - Variables needed for installation (1pp_iwd_cause_wounds_icons and 1pp_cure_spells_conflict) are read from 1pp-config-default.ini or 1pp-config.ini files.                 *
 *        The previous version interrupted installation with plain text prompts allowing players to customise components to their liking (depending on which IE game the mod is     *
 *        installed for and already installed components). All those variables set during installation have been moved into 1pp-config-default.ini file in 1pp folder. This file    *
 *        provides a standard installation. If players want to define their own customized installation, they have to modify the variables involved in 1pp-config-default.ini and   *
 *        save this file as 1pp-config.ini. The installation process will read both ini files and prioritize user values. If a value is not set or mismatched in 1pp-config.ini,    *
 *        the installation process will prevent any installation failure by reverting it back to its default value.                                                                 *
 *      - Coding simplification:                                                                                                                                                    *
 *  		Avoid copying and overwriting the same files again and again...                                                                                                         *
 *  ==============================================================================================================================================================================  */

/*  ============================  *
 *  INDIVIDUALISED ARMOR EFFECTS  *
 *  ============================  */
PRINT @202000 /* Installing individualised armour effects... */

ACTION_FOR_EACH item IN ~1WDGARMR.VVC~ ~1WDSARMR.VVC~ ~GARMORH.BAM~ ~SARMORH.BAM~ BEGIN
	COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
END

// Ghost Armor
ACTION_IF (FILE_EXISTS_IN_GAME ~SPWI317.SPL~) BEGIN
	COPY_EXISTING ~SPWI317.SPL~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
			READ_LONG  0x6a "fx_off"
			READ_SHORT 0x6e "fx_idx"
			READ_SHORT 0x70 "fx_num"
			PATCH_IF (fx_off > 0x71) BEGIN
				FOR (i = fx_idx ; i < fx_num ; ++i) BEGIN
					READ_SHORT (fx_off +        (i * 0x30)) "opcode"
					READ_ASCII (fx_off + 0x14 + (i * 0x30)) "resref"
					PATCH_IF (("%opcode%" = 215) AND ("%resref%" STRING_EQUAL_CASE "ICARMOR")) BEGIN
						WRITE_ASCII (fx_off + 0x14 + (i * 0x30)) ~1WDGARMR~ #8
					END
				END
			END
		END
	BUT_ONLY
END

// Spirit Armor
ACTION_IF (FILE_EXISTS_IN_GAME ~SPWI414.SPL~) BEGIN
	COPY_EXISTING ~SPWI414.SPL~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
			READ_LONG  0x6a "fx_off"
			READ_SHORT 0x6e "fx_idx"
			READ_SHORT 0x70 "fx_num"
			PATCH_IF (fx_off > 0x71) BEGIN
				FOR (i = fx_idx ; i < fx_num ; ++i) BEGIN
					READ_SHORT (fx_off +        (i * 0x30)) "opcode"
					READ_ASCII (fx_off + 0x14 + (i * 0x30)) "resref"
					PATCH_IF (("%opcode%" = 215) AND ("%resref%" STRING_EQUAL_CASE "SPSARMOR")) BEGIN
						WRITE_ASCII (fx_off + 0x14 + (i * 0x30)) ~1WDSARMR~ #8
					END
				END
			END
		END
	BUT_ONLY
END


/*  =================================  *
 *  PnP/IWD STYLE CAUSE WOUNDS SPELLS  *
 *  =================================  */
PRINT @202001 /* Installing PnP/IWD-style 'cause wounds' spells... */

ACTION_FOR_EACH item IN ~1WDCCDAM.BAM~ ~1WDCCDAM.VVC~ ~1WDCLDAM.BAM~ ~1WDCLDAM.VVC~ ~1WDCLWA.bam~ ~1WDCLWB.bam~ ~1WDCLWC.bam~ ~1WDCMDAM.BAM~ ~1WDCMDAM.VVC~ ~1WDCMWA.bam~ ~1WDCMWB.bam~ ~1WDCMWC.bam~ ~1WDCSDAM.BAM~ ~1WDCSDAM.VVC~ BEGIN
	COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
END

// IWD-style icons for 'cause wound' spells (healing icon in red)
ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
	PRINT @202003 /* Copying... */
	ACTION_FOR_EACH item IN ~SPPR414A.bam~ ~SPPR414B.bam~ ~SPPR414C.bam~ ~SPPR510A.bam~ ~SPPR510B.bam~ ~SPPR510C.bam~ BEGIN	// Cause Serious and Critical Wounds
		COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
	END
END

// Cause Serious Wounds, Cause Critical Wounds and Harm
ACTION_FOR_EACH ~spell~ IN ~SPPR414~ ~SPPR510~ ~SPPR608~ BEGIN

	OUTER_SET uname = 0

	ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN
		COPY_EXISTING ~%spell%.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				READ_LONG 0x8 uname
			END
		BUT_ONLY

		COPY ~1pp/item/spells_and_scrolls/tweak/%spell%.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				WRITE_LONG 0x8 uname
				PATCH_MATCH ~%spell%~ WITH
					SPPR414 BEGIN SAY 0x50 @202101 /* Cause Serious Wounds (Necromancy)
Level: 4
Sphere: Healing
Range: Touch
Duration: Permanent
Casting Time: 7
Area of Effect: Creature touched
Saving Throw: 1/2

This spell is the reverse of cure serious wounds. Instead of healing the creature touched, it causes damage. On a successful touch from the priest, the spell inflicts 17 damage upon the target. A successful saving throw against spells halves the damage. */ END
					SPPR510 BEGIN SAY 0x50 @202102 /* Cause Critical Wounds (Necromancy)
Level: 5
Sphere: Healing
Range: Touch
Duration: Permanent
Casting Time: 8
Area of Effect: Creature touched
Saving Throw: 1/2

This spell is the reverse of cure critical wounds. Instead of healing the creature touched, it causes damage. On a successful touch from the priest, the spell inflicts 27 damage upon the target. A successful saving throw against spells halves the damage. */ END
					SPPR608 BEGIN SAY 0x50 @202103 /* Harm (Necromancy)
Level: 6
Sphere: Healing
Range: Touch
Duration: Permanent
Casting Time: 1 round
Area of Effect: Creature touched
Saving Throw: Special

This spell is the reverse of Heal. Instead of healing the creature touched, it causes damage. On a successful touch from the priest, the target will suffer 1d6 points of damage per level of the caster, up to a maximum of 15d6 points of damage at 15th level. If the target fails its saving throw, its remaining hit points will be reduced to 1. */ END
					DEFAULT
				END	// of PATCH_MATCH ~%spell%~ WITH
			END

	END				// of ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~)

END					// of ACTION_FOR_EACH ~spell~ IN ~SPPR414~ ~SPPR510~ ~SPPR608~


// Cause Light Wounds and Cause Medium Wounds.
ACTION_IF (FILE_EXISTS_IN_GAME ~SPPR142.SPL~ OR FILE_EXISTS_IN_GAME ~SPPR342.SPL~) BEGIN

	ACTION_IF (1pp_cure_spells_conflict = 2) BEGIN
		FAIL @202005 /* Aborted. */
	END

END

ADD_SPELL ~1pp/item/spells_and_scrolls/tweak/SPPR142.SPL~ 1 1 ~CLERIC_CAUSE_LIGHT_WOUNDS~
	SAY 0x8  @202104 /* Cause Light Wounds */
	SAY 0x50 @202105 /* Cause Light Wounds (Necromancy)
Level: 1
Sphere: Healing
Range: Touch
Duration: Permanent
Casting Time: 5
Area of Effect: Creature touched
Saving Throw: 1/2

This spell is the reverse of cure light wounds. Instead of healing the creature touched, it causes damage. On a successful touch from the priest, the spell inflicts 8 damage upon the target. A successful saving throw against spells halves the damage. */

ADD_SPELL ~1pp/item/spells_and_scrolls/tweak/SPPR342.SPL~ 1 3 ~CLERIC_CAUSE_MEDIUM_WOUNDS~
	SAY 0x8  @202106 /* Cause Medium Wounds */
	SAY 0x50 @202107 /* Cause Medium Wounds (Necromancy)
Level: 3
Sphere: Healing
Range: Touch
Duration: Permanent
Casting Time: 5
Area of Effect: Creature touched
Saving Throw: 1/2

This spell is the reverse of cure medium wounds. Instead of healing the creature touched, it causes damage. On a successful touch from the priest, the spell inflicts 13 damage upon the target. A successful saving throw against spells halves the damage. */

//END of COMPONENT