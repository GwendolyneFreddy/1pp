/* ================================================================================================================================================================================= *
 *  v 4.2.0                                                                                                                                                                          *
 *                                                                  C O M P O N E N T   :   S p e l l   t w e a k s                                                                  *
 *                                                                                                                                                                                   *
 * ================================================================================================================================================================================= *
 *  This component installs minor spell tweaks for SoA/ToB, giving armour spells distinct appearances (ghost armour, spirit armour) as well as reworked 'cause wounds' type of       *
 *  spells, bringing them into line with their p&p equivalents and actually making them viable.                                                                                      *
 * ================================================================================================================================================================================= *
 *  change-log:                                                                                                                                                                      *
 *  -----------                                                                                                                                                                      *
 *      - Replaced READLN action ~WARNING: SPPR142/1342 (Cause Light Wounds, Cause Medium Wounds) seem to be already taken by a previously installed mod. Proceeding to install may  *
 *        cause issues.~ with a new code using !FILE_CONTAINS_EVALUATED(~spell.ids~ ~~). This will avoid to overwrite existing spells.                                               *
 *      - Variable needed for installation (1pp_iwd_cause_wounds_icons) is read from 1pp-config-default.ini or 1pp-config-user.ini files.                                            *
 *        The previous version interrupted installation with plain text prompts allowing players to customise components to their liking (depending on which IE game the mod is      *
 *        installed for and already installed components). All those variables set during installation have been moved into 1pp-config-default.ini file in 1pp folder. This file     *
 *        provides a standard installation. If players want to define their own customized installation, they have to modify the variables involved in 1pp-config-default.ini and    *
 *        save it as 1pp-config-user.ini. The installation process will read both .ini files and prioritize user values. If a value is not set or mismatched in user's file          *
 *        1pp-config-user.ini, the installation process will prevent any installation failure by reverting it back to its default value.                                             *
 *      - Coding simplification:                                                                                                                                                     *
 *          Avoid copying and overwriting the same files again and again...                                                                                                          *
 *          Replaced BWP Fixpack Lollorian's patchified Armor animation changes {modifies op#215 visual effect in ALL spell headers) with WeiDU innate ALTER_EFFECT function.        *
 *          Added IWDIfication and SR compatibility for Installing PnP/IWD-style 'cause wounds' spells in a rather softer way.                                                       *
 *                                                                                                                                                                                   *
 *  specific fixes:                                                                                                                                                                  *
 *  ---------------                                                                                                                                                                  *
 *      - #41  ==> spwi317.spl (Ghost Armor): added SR compatibility for individualised armour effects. 1PP patches also op#215 with GARMORH resource.                               *
 * ================================================================================================================================================================================= */

/* ============================== *
 *  INDIVIDUALISED ARMOR EFFECTS  *
 * ============================== */

PRINT @202000 // ~Installing individualised armour effects...~
SILENT

ACTION_FOR_EACH item IN ~1wdgarmr.vvc~ ~1wdsarmr.vvc~ ~garmorh.bam~ ~sarmorh.bam~ BEGIN
	COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
END

// Ghost Armor
ACTION_IF (FILE_EXISTS_IN_GAME ~spwi317.spl~) BEGIN
	COPY_EXISTING ~spwi317.spl~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
			LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICARMOR resource = 1WDGARMR END
			LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = GARMORH resource = 1WDGARMR END // SR compatibility
/*			READ_LONG  0x6a "fx_off"
			READ_SHORT 0x6e "fx_idx"
			READ_SHORT 0x70 "fx_num"
			PATCH_IF (fx_off > 0x71) BEGIN
				FOR (i = fx_idx ; i < fx_num ; ++i) BEGIN
					READ_SHORT (fx_off +        (i * 0x30)) "opcode"
					READ_ASCII (fx_off + 0x14 + (i * 0x30)) "resref"
					PATCH_IF (("%opcode%" = 215) AND ("%resref%" STRING_EQUAL_CASE "ICARMOR")) BEGIN
						WRITE_ASCII (fx_off + 0x14 + (i * 0x30)) ~1WDGARMR~ #8
					END
				END
			END*/
		END
	BUT_ONLY
END

// Spirit Armor
ACTION_IF (FILE_EXISTS_IN_GAME ~spwi414.spl~) BEGIN
	COPY_EXISTING ~spwi414.spl~ ~override~
		PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
			LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = SPSARMOR resource = 1WDSARMR END
/*			READ_LONG  0x6a "fx_off"
			READ_SHORT 0x6e "fx_idx"
			READ_SHORT 0x70 "fx_num"
			PATCH_IF (fx_off > 0x71) BEGIN
				FOR (i = fx_idx ; i < fx_num ; ++i) BEGIN
					READ_SHORT (fx_off +        (i * 0x30)) "opcode"
					READ_ASCII (fx_off + 0x14 + (i * 0x30)) "resref"
					PATCH_IF (("%opcode%" = 215) AND ("%resref%" STRING_EQUAL_CASE "SPSARMOR")) BEGIN
						WRITE_ASCII (fx_off + 0x14 + (i * 0x30)) ~1WDSARMR~ #8
					END
				END
			END*/
		END
	BUT_ONLY
END


/* =================================== *
 *  PnP/IWD STYLE CAUSE WOUNDS SPELLS  *
 * =================================== */

ACTION_IF NOT SR_INSTALLED BEGIN

	PRINT @202001 // ~Installing PnP/IWD-style 'cause wounds' spells...~
	SILENT

	ACTION_FOR_EACH item IN ~1wdccdam.bam~ ~1wdccdam.vvc~ ~1wdcsdam.bam~ ~1wdcsdam.vvc~ BEGIN
		COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
	END

	// Cause Serious Wounds, Cause Critical Wounds and Harm
	ACTION_FOR_EACH ~spell~ IN ~sppr414~ ~sppr510~ ~sppr608~ BEGIN

		OUTER_SET uname = 0

		ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~) BEGIN
			COPY_EXISTING ~%spell%.spl~ ~override~
				PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
					READ_LONG 0x8 uname
				END
			BUT_ONLY

			COPY ~1pp/item/spells_and_scrolls/tweak/%spell%.spl~ ~override~
				PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
					WRITE_LONG 0x8 uname
					PATCH_MATCH ~%spell%~ WITH
						sppr414 BEGIN SAY 0x50 @202101 END
						sppr510 BEGIN SAY 0x50 @202102 END
						sppr608 BEGIN SAY 0x50 @202103 END
						DEFAULT
					END	// of PATCH_MATCH ~%spell%~ WITH
				END

		END				// of ACTION_IF (FILE_EXISTS_IN_GAME ~%spell%.spl~)

	END					// of ACTION_FOR_EACH ~spell~ IN ~SPPR414~ ~SPPR510~ ~SPPR608~

	// IWD-style icons for 'cause wound' spells (healing icon in red)
	ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
		PRINT @202002 // ~Installing IWD-style icons for 'cause wound' spells...~
		SILENT
		ACTION_FOR_EACH item IN ~sppr414a~ ~sppr414b~ ~sppr414c~ ~sppr510a~ ~sppr510b~ ~sppr510c~ BEGIN	// Cause Serious and Critical Wounds
			COPY ~1pp/item/spells_and_scrolls/tweak/%item%.bam~ ~override~
		END
	END

	// Cause Light Wounds
	ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_LIGHT_WOUNDS~)) BEGIN

		PRINT @202003 // ~Installing new 'Cause Light Wounds' spell.~
		SILENT
		ACTION_FOR_EACH item IN ~1wdcldam.bam~ ~1wdcldam.vvc~ ~1wdclwa.bam~ ~1wdclwb.bam~ ~1wdclwc.bam~ BEGIN
			COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
		END

		ADD_SPELL ~1pp/item/spells_and_scrolls/tweak/sppr142.spl~ 1 1 ~CLERIC_CAUSE_LIGHT_WOUNDS~
			SAY NAME1 @202104
			SAY UNIDENTIFIED_DESC @202105

	END

	// Cause Medium Wounds
	ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CAUSE_MEDIUM_WOUNDS~)) BEGIN

		PRINT @202004 // ~Installing new 'Cause Medium Wounds' spell.~
		SILENT
		ACTION_FOR_EACH item IN ~1wdcmdam.bam~ ~1wdcmdam.vvc~ ~1wdcmwa.bam~ ~1wdcmwb.bam~ ~1wdcmwc.bam~ BEGIN
			COPY ~1pp/item/spells_and_scrolls/tweak/%item%~ ~override~
		END

		ADD_SPELL ~1pp/item/spells_and_scrolls/tweak/SPPR342.SPL~ 1 3 ~CLERIC_CAUSE_MEDIUM_WOUNDS~
			SAY NAME1 @202106
			SAY UNIDENTIFIED_DESC @202107

	END

END ELSE BEGIN // ACTION_IF SR_INSTALLED BEGIN

	// Cause Light Wounds
	ACTION_IF (FILE_EXISTS_IN_GAME ~dvpr119d.spl~) BEGIN

		PRINT @202005 // ~Spell Revisions detected: patching 'Cause Light Wounds' spell.~
		SILENT
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcldam.bam~ ~override~
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcldam.vvc~ ~override~

		COPY_EXISTING ~dvpr119d.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICMAGICH resource = 1WDCLDAM END
			END
		BUT_ONLY

		// IWD-style icons for 'cause wound' spells (healing icon in red)
		ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
			PRINT @202002 // ~Installing IWD-style icons for 'cause wound' spells...~
			SILENT
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdclwa.bam~ ~override/dvpr119a.bam~
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdclwb.bam~ ~override/dvpr119b.bam~
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdclwc.bam~ ~override/dvpr119c.bam~
		END

	END

	// Cause Moderate Wounds (new cleric spell)
	ACTION_IF (FILE_EXISTS_IN_GAME ~dvpr219d.spl~) BEGIN

		PRINT @202006 // ~Spell Revisions detected: patching 'Cause Moderate Wounds' spell.~
		SILENT
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcmdam.bam~ ~override~
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcmdam.vvc~ ~override~

		COPY_EXISTING ~dvpr219d.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICMAGICH resource = 1WDCMDAM END
			END
		BUT_ONLY

		ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
			PRINT @202002 // ~Installing IWD-style icons for 'cause wound' spells...~
			SILENT
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdcmwa.bam~ ~override/sppr219a.bam~
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdcmwb.bam~ ~override/sppr219b.bam~
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdcmwc.bam~ ~override/sppr219c.bam~
		END

	END

	// Cause Serious Wounds (moved from lvl 4) - sppr414 icons
	ACTION_IF (FILE_EXISTS_IN_GAME ~dvpr321d.spl~) BEGIN

		PRINT @202007 // ~Spell Revisions detected: patching 'Cause Serious Wounds' spell.~
		SILENT
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcsdam.bam~ ~override~
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdcsdam.vvc~ ~override~

		COPY_EXISTING ~dvpr321d.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICMAGICH resource = 1WDCSDAM END
			END
		BUT_ONLY

		ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
			PRINT @202002 // ~Installing IWD-style icons for 'cause wound' spells...~
			SILENT
			ACTION_FOR_EACH item IN ~sppr414a~ ~sppr414b~ ~sppr414c~ BEGIN	// Cause Serious Wounds
				COPY ~1pp/item/spells_and_scrolls/tweak/%item%.bam~ ~override~
			END
		END

	END

	// Cause Critical Wounds (moved from lvl 5, replaces Cause Serious Wounds) - sppr510 icons
	ACTION_IF (FILE_EXISTS_IN_GAME ~sppr414d.spl~) BEGIN

		PRINT @202008 // ~Spell Revisions detected: patching 'Cause Critical Wounds' spell.~
		SILENT
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdccdam.bam~ ~override~
		COPY ~1pp/item/spells_and_scrolls/tweak/1wdccdam.vvc~ ~override~

		COPY_EXISTING ~sppr414d.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICMAGICH resource = 1WDCCDAM END
			END
		BUT_ONLY

		ACTION_IF (1pp_iwd_cause_wounds_icons = 1) BEGIN
			PRINT @202002 // ~Installing IWD-style icons for 'cause wound' spells...~
			SILENT
			ACTION_FOR_EACH item IN ~sppr510a~ ~sppr510b~ ~sppr510c~ BEGIN	// Cause Critical Wounds
				COPY ~1pp/item/spells_and_scrolls/tweak/%item%.bam~ ~override~
			END
		END

	END

	// Harm
	ACTION_IF (FILE_EXISTS_IN_GAME ~sppr608d.spl~) BEGIN

		PRINT @202009 // ~Spell Revisions detected: patching 'Harm' spell.~
		SILENT
		ACTION_IF (!FILE_EXISTS_IN_GAME ~1wdccdam.bam~) BEGIN
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdccdam.bam~ ~override~
			COPY ~1pp/item/spells_and_scrolls/tweak/1wdccdam.vvc~ ~override~
		END

		COPY_EXISTING ~sppr608d.spl~ ~override~
			PATCH_IF (SOURCE_SIZE > 0x71) BEGIN // protects against invalid files
				LPF ALTER_EFFECT INT_VAR silent = 1 check_globals = 0 match_opcode = 215 STR_VAR match_resource = ICMAGICH resource = 1WDCCDAM END
			END
		BUT_ONLY

	END

END

// END of COMPONENT


